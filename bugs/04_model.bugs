
     model{
     #priors (psi)                      #no covariates on psi
	  beta~dnorm(0,0.368)
	
	#priors (R)		        	   #no covariates on R
	  beta2~dnorm(0,0.368) 
                               
        #priors (p)                     #2 covariates on p (+ 2 intercepts)
          for(zz in 1:4){         
            gamma[zz]~dnorm(0,0.368)
          }

	#priors (delta)                    #no covariate on delta: seasondate
	  iota~dnorm(0,0.368)
	    
        #likelihood
          for(ii in 1:nhex){           #loop through hexagons         
        
          #categorical 1 draw from a multinomial
          Occ.hex[ii]~dcat(TPM[ii, ])  #occupancy state of hexagon: 3=empty, 2=occupied with reproduction, 1=occupied without reproduction
  
          TPM[ii,1]<-psi[ii]*(1-R[ii]) #occupied without paired birds
          TPM[ii,2]<-psi[ii]*R[ii]     #occupied with paired birds    
          TPM[ii,3]<-1-psi[ii]         #unoccupied 
  
	    #logistic regression of occupancy probability of hexagon
          	logit(psi[ii])<-beta

	    #logistic regression of paired status probability, given occupancy of hexagon
	        logit(R[ii])<-beta2
  
          for(kk in 1:nVisits){ #loop through visits at recorders
              y[ii,kk]~dcat(p[ii,kk,Occ.hex[ii], ]) #encounter data
  
              p[ii,kk,1,1]<-pA[ii,kk]     #detect occupancy
              p[ii,kk,1,2]<-0             #can't detect reproduction if no reproduction occurs
              p[ii,kk,1,3]<-1-pA[ii,kk]   #didn't detect occupancy
    
              p[ii,kk,2,1]<-pJ[ii,kk]*(1-delta[ii,kk])    #detect occupancy and failed to detect paired status
              p[ii,kk,2,2]<-pJ[ii,kk]*delta[ii,kk]        #detect occupancy and paired status 
              p[ii,kk,2,3]<-1-pJ[ii,kk]                   #failed to detect occupancy
    
              p[ii,kk,3,1]<-0
              p[ii,kk,3,2]<-0
              p[ii,kk,3,3]<-1 #can't detect occupancy or reproduction if they are not there
            
            #logistic regression of detection probability of species without paired birds:
               logit(pA[ii,kk])<-gamma[1]            + gamma[3]*noise[ii,kk] + gamma[4]*effort[ii,kk]    
              
            #logistic regression of detection probability of species with paired birds:
               logit(pJ[ii,kk])<-gamma[1] + gamma[2] + gamma[3]*noise[ii,kk] + gamma[4]*effort[ii,kk]    
              
            #logistic regression of probability of correctly detecting paired status:  
               logit(delta[ii,kk])<-iota
             
            } #kk
        } #ii
        
        ## detection derived params
        
        logit(pOcc) <- gamma[1]           
        logit(pPair) <- gamma[1] + gamma[2]   
        
		pOcc2 <- 1 - ((1-pOcc)^2)
		pOcc3 <- 1 - ((1-pOcc)^3)
		pOcc4 <- 1 - ((1-pOcc)^4)
		pOcc5 <- 1 - ((1-pOcc)^5)
		pOcc6 <- 1 - ((1-pOcc)^6)

		pPair2 <- 1 - ((1-pPair)^2)
		pPair3 <- 1 - ((1-pPair)^3)
		pPair4 <- 1 - ((1-pPair)^4)
		pPair5 <- 1 - ((1-pPair)^5)
		pPair6 <- 1 - ((1-pPair)^6)


        logit(condPair) <- iota
        truePair <- pPair * condPair

		condPair2 <- 1 - ((1-condPair)^2)
		condPair3 <- 1 - ((1-condPair)^3)
		condPair4 <- 1 - ((1-condPair)^4)
		condPair5 <- 1 - ((1-condPair)^5)
		condPair6 <- 1 - ((1-condPair)^6)
	
		truePair2 <- 1 - ((1 - (condPair*pPair))^2)
		truePair3 <- 1 - ((1 - (condPair*pPair))^3)
		truePair4 <- 1 - ((1 - (condPair*pPair))^4)
		truePair5 <- 1 - ((1 - (condPair*pPair))^5)
		truePair6 <- 1 - ((1 - (condPair*pPair))^6)


	## occupancy derived params

	logit(psiD) <- beta[1]
	logit(RD) <- beta2[1]

	occNoPair <- psiD * (1 - RD)
	occPairs <- psiD * RD
	unOcc <- 1 - psiD
  
     }#end of model
      
